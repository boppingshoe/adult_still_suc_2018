
model{
  
  for (i in 1:n_ind){
    # GLM
    det[i]~ dbern(phi[i])
    logit(phi[i])<- b_0+ b_juld*juld[i]+ b_temp*temp[i]+ b_ftt*ftt[i]+
      w*b_txf*temp[i]*ftt[i]+ b_dis*dis[i]+ b_trans*trans[i]+ a_yr[yr[i]]
    logit(phi_rep[i])<- b_0+ b_juld*juld[i]+ b_temp*temp[i]+ b_ftt*ftt[i]+
      w*b_txf*temp[i]*ftt[i]+ b_dis*dis[i]+ b_trans*trans[i]+ a_yr[yr[i]]

    # FTT
    ftt[i]<- (225/vel[i]- 7.88)/ 4.29 # scaled ftt 
    vel[i]~ dnorm(mu_v, tau_v)T(0,)
  }

  # priors
  # GLM
  b_0~ dt(0, 0.1, 1)
  b_juld~ dt(0, 0.4, 1)
  b_temp~ dt(0, 0.4, 1)
  b_ftt~ dt(0, 0.4, 1)
  b_txf~ dt(0, 0.4, 1)
  b_dis~ dt(0, 0.4, 1)
  b_trans~ dt(0, 0.4, 1)
  for (j in 1:15){
    a_yr[j]~ dnorm(0, tau_yr)
  }
  sigma_yr~ dt(0, 0.444, 1)T(0,5)
  tau_yr<- pow(sigma_yr, -2)
  w~ dbern(0.5)

  # FTT
  mu_v~ dt(0, 0.1, 1)T(0,)
  sigma_v~ dt(0, 0.444, 1)T(0,)
  tau_v<- pow(sigma_v, -2)

  # posterior predictive check
  phi_fit<- mean(phi_rep)

}
